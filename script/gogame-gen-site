#!/usr/bin/env perl
use GoGameTools::features;
use GoGameTools::Log;
use GoGameTools::Parser::FilterQuery;
use GoGameTools::Plumbing;
use GoGameTools::Porcelain::SiteGenData;
use GoGameTools::Porcelain::SiteMungeSGJ;
use GoGameTools::Porcelain::SiteWrite;
use GoGameTools::TagHandler;
use GoGameTools::Util;
my %opt = get_options(
    expect_stdin => 1,
    extra_opts   => [
        qw(
          annotations=s
          basedir=s
          cachedir=s
          delete-metadata
          delete-private-refs
          delete-secret-problems
          filter=s
          menu=s@
          nopermalinks
          site-dir|d=s
          viewer=s
          www-dir=s
          zip=s
          )
    ],
    required => [qw(viewer www-dir)]
);
my $viewer_class = load_viewer_class($opt{viewer});
my ($pipe_filter, $pipe_write);
if (defined $opt{filter}) {
    my $expr = parse_filter_query($opt{filter})
      or fatal("can't parse query $opt{filter}");
    $pipe_filter = sub ($collection) {
        return [ grep { eval_query(expr => $expr, vars => query_vars_from_sgj($_)) }
              $collection->@* ];
    };
}

# If --www-dir is used, generate the web site. If --zip is used, generate the
# zip file for EasyGo.
if (defined $opt{'www-dir'}) {
    $pipe_write = GoGameTools::Porcelain::SiteWrite->new(
        delete_metadata => $opt{'delete-metadata'},
        site_dir        => $opt{'site-dir'},
        viewer_delegate => $viewer_class->new,
        www_dir         => $opt{'www-dir'},
    )->run;
} elsif (defined $opt{zip}) {
    $pipe_write = GoGameTools::Porcelain::EasyGoWrite->new(zip => $opt{zip})->run;
} else {
    fatal("use either ---www-dir or --zip");
}
register_tags();
run_pipe(
    pipe_flex_stdin_to_trees(),
    pipe_convert_markup_to_directives(),
    pipe_convert_directives_from_comment(),
    (defined $opt{annotations} ? pipe_annotate($opt{annotations}) : ()),
    pipe_gen_problems(
        cache_dir       => $opt{cachedir},
        viewer_delegate => $viewer_class->new
    ),
    pipe_trees_to_sgj(),

    # munge the SGJ collection
    GoGameTools::Porcelain::SiteMungeSGJ->new(
        basedir                => $opt{basedir},
        delete_private_refs    => $opt{'delete-private-refs'},
        delete_secret_problems => $opt{'delete-secret-problems'},
    )->run,

    # possibly filter
    (defined $opt{filter} ? $pipe_filter : ()),

    # generate the site data
    GoGameTools::Porcelain::SiteGenData->new(
        menu          => $opt{menu},
        no_permalinks => $opt{nopermalinks},
    )->run,
    $pipe_write,
);
