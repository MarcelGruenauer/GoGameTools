#!/usr/bin/env perl
use GoGameTools::features;
use GoGameTools::Util;
use File::Temp qw(tempfile tempdir);
use constant TYPE_SITE_STATIC  => 'site-static';
use constant TYPE_SITE_DYNAMIC => 'site-dynamic';
use constant TYPE_EASYGO       => 'easygo';
my %valid_type =
  map { $_ => 1 } (TYPE_SITE_STATIC, TYPE_SITE_DYNAMIC, TYPE_EASYGO);
my $type = shift // '';

unless ($valid_type{$type}) {
    die sprintf "Usage: %s [%s] [args]\n", $0, join('|', keys %valid_type);
}
my @extra_opts = qw(src=s query|q=s menu|m=s@ metadata gameinfo deprivatize);
if ($type eq TYPE_SITE_STATIC) {
    push @extra_opts, qw(www=s viewer=s open);
} elsif ($type eq TYPE_SITE_DYNAMIC) {
    push @extra_opts, qw(www=s viewer=s serve);
}
my %opt = get_options(
    extra_opts => \@extra_opts,
    required   => [qw(src)],
);
unless (-d $opt{src}) {
    die "$opt{src} is not a directory\n";
}
if ($type eq TYPE_SITE_STATIC || $type eq TYPE_SITE_DYNAMIC) {
    $opt{viewer} //= 'WGo';
    $opt{www} //= tempdir(CLEANUP => 0);
} elsif ($type eq TYPE_EASYGO) {
    $opt{viewer} = 'EasyGo';
}
say "GOGAME_SRC=$opt{src}";
say "GOGAME_ANNOTATIONS=$opt{src}/annotations.tsv";
say 'rm -f $GOGAME_SRC/kombilo.d[ab]';
say 'gogame-patterns-annotate -d $GOGAME_SRC >$GOGAME_ANNOTATIONS';
say 'rm -f $GOGAME_SRC/kombilo.d[ab]';    # cleanup

# optional arguments that can be passed to gogame-* scripts
my (@gen_problems_args, @munge_refs_args, @site_gen_data_args);
push @gen_problems_args,  '--annotations $GOGAME_ANNOTATIONS';
push @gen_problems_args,  '--metadata' if $opt{metadata};
push @gen_problems_args,  '--gameinfo' if $opt{gameinfo};
push @munge_refs_args,    '--deprivatize' if $opt{deprivatize};
push @site_gen_data_args, map { "-m $_" } $opt{menus}->@* if $opt{menus};
my @pipe = (
    'find $GOGAME_SRC -name \*.sgf',
    "gogame-gen-problems --viewer $opt{viewer} @gen_problems_args",
    "gogame-munge-refs @munge_refs_args",
    (defined($opt{query}) ? "gogame-filter -q '$opt{query}'" : ()),
    "gogame-site-gen-data @site_gen_data_args"
);

if ($type eq TYPE_SITE_STATIC) {
    local $" = ' ';
    say "GOGAME_WWW=$opt{www}";
    say join(' | ',
        @pipe, "gogame-site-write-static --viewer $opt{viewer} -d \$GOGAME_WWW");
    if ($opt{open}) {
        say 'open $GOGAME_WWW/index.html';
    }
} elsif ($type eq TYPE_SITE_DYNAMIC) {
    local $" = ' ';
    say "GOGAME_WWW=$opt{www}";
    say join(' | ',
        @pipe, "gogame-site-write-dynamic --viewer $opt{viewer} -d \$GOGAME_WWW");
    if ($opt{serve}) {
        say 'echo Open the site at http://0.0.0.0:8888';
        say 'cd $GOGAME_WWW && python -m SimpleHTTPServer 8888';
    }
} elsif ($type eq TYPE_EASYGO) {

    # date
    my ($min, $hour, $mday, $month, $year) = (localtime())[ 1 .. 5 ];
    $year += 1900;
    $month++;
    my $date = sprintf "%04d.%02d.%02d-%02d%02d", $year, $month, $mday, $hour, $min;
    my $zip_file = "$ENV{HOME}/Desktop/easygo-$date.zip";
    say join(' | ', @pipe, "gogame-site-easygo -z $zip_file");
    say "echo $zip_file";
}

=pod

    gogame-run site-static --src ... | sh
    gogame-run site-dynamic --src ... | sh
    gogame-run easygo --src ... | sh

=cut
